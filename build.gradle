plugins {
    id 'java'
    // ASSUMES GRADLE 5.6 OR HIGHER. Use plugin version 0.8.10 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.18'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'

    id "io.freefair.lombok" version "6.5.0.3"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
}

def grpcVersion = '1.48.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.21.1'
def protocVersion = protobufVersion

dependencies {
    implementation 'io.dapr:dapr-sdk:1.6.0'
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'org.projectlombok:lombok:1.18.24'

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"


    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"


    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// IntelliJ aparently knows how to figure out the whole source thing but
// VS code was having a hard time finding gRPC-related java generated 
// source files. Explicitely setting source seems do do the trick.
// https://stackoverflow.com/questions/72440517/

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir "${protobuf.generatedFilesBaseDir}/main/java"
            srcDir "${protobuf.generatedFilesBaseDir}/main/grpc"
        }

        proto {
            srcDir "${rootProject.projectDir}/proto"
        }

        resources {
            srcDir 'src/main/resources'
        }
    }
}

test {
    useJUnitPlatform()
}